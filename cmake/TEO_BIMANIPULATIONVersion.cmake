include(GitInfo) # YCM

# Define current version.
set(TEO_BIMANIPULATION_VERSION_MAJOR 0)
set(TEO_BIMANIPULATION_VERSION_MINOR 1)
set(TEO_BIMANIPULATION_VERSION_PATCH 0)

set(TEO_BIMANIPULATION_VERSION
    ${TEO_BIMANIPULATION_VERSION_MAJOR}.${TEO_BIMANIPULATION_VERSION_MINOR}.${TEO_BIMANIPULATION_VERSION_PATCH})

set(TEO_BIMANIPULATION_VERSION_SHORT ${TEO_BIMANIPULATION_VERSION})

# Retrieve current revision from Git working tree.
git_wt_info(SOURCE_DIR "${CMAKE_SOURCE_DIR}"
            PREFIX TEO_BIMANIPULATION)

if(DEFINED TEO_BIMANIPULATION_GIT_WT_HASH)
    if(TEO_BIMANIPULATION_GIT_WT_TAG_REVISION GREATER 0)
        set(TEO_BIMANIPULATION_VERSION_REVISION ${TEO_BIMANIPULATION_GIT_WT_TAG_REVISION})
        string(REPLACE "-" "" _date ${TEO_BIMANIPULATION_GIT_WT_AUTHOR_DATE})
        set(TEO_BIMANIPULATION_VERSION_SOURCE
            "${_date}.${TEO_BIMANIPULATION_GIT_WT_DATE_REVISION}+git${TEO_BIMANIPULATION_GIT_WT_HASH_SHORT}")
    endif()

    if(TEO_BIMANIPULATION_GIT_WT_DIRTY)
        set(TEO_BIMANIPULATION_VERSION_DIRTY "dirty")
    endif()
endif()

if(DEFINED TEO_BIMANIPULATION_VERSION_SOURCE)
    if(NOT "${TEO_BIMANIPULATION_GIT_WT_TAG}" STREQUAL "v${TEO_BIMANIPULATION_VERSION_SHORT}")
        set(TEO_BIMANIPULATION_VERSION
            "${TEO_BIMANIPULATION_VERSION_SHORT}+${TEO_BIMANIPULATION_VERSION_SOURCE}")
    else()
        set(TEO_BIMANIPULATION_VERSION
           "${TEO_BIMANIPULATION_VERSION_SHORT}+${TEO_BIMANIPULATION_VERSION_REVISION}-${TEO_BIMANIPULATION_VERSION_SOURCE}")
    endif()
elseif(NOT "${TEO_BIMANIPULATION_GIT_WT_TAG}" STREQUAL "v${TEO_BIMANIPULATION_VERSION_SHORT}")
    set(TEO_BIMANIPULATION_VERSION "${TEO_BIMANIPULATION_VERSION_SHORT}~dev")
else()
    set(TEO_BIMANIPULATION_VERSION "${TEO_BIMANIPULATION_VERSION_SHORT}")
endif()

if(DEFINED TEO_BIMANIPULATION_VERSION_DIRTY)
    set(TEO_BIMANIPULATION_VERSION "${TEO_BIMANIPULATION_VERSION}+${TEO_BIMANIPULATION_VERSION_DIRTY}")
endif()

# Print version.
message(STATUS "TEO_BIMANIPULATION version: ${TEO_BIMANIPULATION_VERSION_SHORT} (${TEO_BIMANIPULATION_VERSION})")
